plugins {
    id 'scala'
    id 'idea'
    id 'distribution'
    id 'de.undercouch.download' version '2.1.0'
}

import de.undercouch.gradle.tasks.download.Download

version = '0.9.4.0'

repositories {
    mavenCentral()
}

def jettyVersion = "9.0.4.v20130625"

dependencies {
    compile "org.scala-lang:scala-library:${scalaVersion}.+"
    compile "org.apache.mesos:mesos:${mesosVersion}"
    compile "com.google.protobuf:protobuf-java:2.5.0"
    compile "log4j:log4j:1.2.17"
    compile "net.sf.jopt-simple:jopt-simple:4.8"
    compile "org.eclipse.jetty:jetty-http:${jettyVersion}"
    compile "org.eclipse.jetty:jetty-io:${jettyVersion}"
    compile "org.eclipse.jetty:jetty-security:${jettyVersion}"
    compile "org.eclipse.jetty:jetty-server:${jettyVersion}"
    compile "org.eclipse.jetty:jetty-servlet:${jettyVersion}"
    compile "org.eclipse.jetty:jetty-util:${jettyVersion}"
    compile "javax.servlet:javax.servlet-api:3.0.1"
    testCompile 'junit:junit:4.12'

    compile ("org.apache.kafka:kafka_${scalaVersion}:${kafkaVersion}") {
        exclude group: "javax.jms"
        exclude group: "com.sun.jdmk"
        exclude group: "com.sun.jmx"
        exclude group: "junit"
        exclude group: "jline"
    }
}

jar {
    dependsOn 'test'
    doFirst {
        from(configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
            exclude "*"
            exclude "about_files/*"
            exclude "META-INF/*.SF"
            exclude "META-INF/*.DSA"
            exclude "META-INF/*.RSA"
        }
    }
    manifest.attributes("Main-Class": "ly.stealth.mesos.kafka.Cli")
}

distributions.main {
    contents {
        from(jar.destinationDir) {
            include jar.archiveName
        }
        from(projectDir) {
            include "README.md"
            include "LICENSE"
            include "kafka-mesos.properties"
            include "kafka-mesos.sh"
        }
    }
}

distTar {
    dependsOn jar
    compression Compression.GZIP
}

task copyJarToRoot(type: Copy) {
    into rootDir
    from(jar.destinationDir) {
        include jar.archiveName
    }
}

task downloadKafka(type: Download) {
    src "https://archive.apache.org/dist/kafka/${kafkaVersion}/kafka_${scalaVersion}-${kafkaVersion}.tgz"
    dest rootDir
}

build.dependsOn copyJarToRoot

clean.doFirst {
    delete "${copyJarToRoot.destinationDir}/${jar.archiveName}"
}
